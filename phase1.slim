// Phase 1: Load burn-in
// Allow recessive deleterious mutations to spread throughout the population

initialize() {

    // Parameters
    defineConstant("K_ANCESTRAL", 5000);      // Population size
    defineConstant("L", 23e6);                // Chromosome 2L
    defineConstant("REC_RATE", 2.4e-8);       // Recombination rate
    defineConstant("INV_START", 2e6);
    defineConstant("INV_END", 13e6);          // Inversion In(2L)t -- 11 Mb
    defineConstant("MU_TOTAL", 3e-9);
    defineConstant("DFE_SHAPE", 0.33);
    defineConstant("DFE_MEAN_S", -0.001);     // Mean deleterious effect
    defineConstant("H_DOMINANCE", 0.2);       // Recessive
    defineConstant("BURN_IN_GENS", 10 * K_ANCESTRAL);

    // Model setup
    initializeSLiMModelType("WF");

    // Neutral mutation
    initializeMutationType("m1", 0.5, "f", 0.0);
    m1.convertToSubstitution = F;

    // Deleterious mutation
    initializeMutationType("m2", H_DOMINANCE, "g",
                           DFE_MEAN_S, DFE_SHAPE);
    m2.convertToSubstitution = F;

    // Inversion marker
    initializeMutationType("m3", 0.5, "f", 0.0);
    m3.convertToSubstitution = F;

    defineConstant("INV_MARKER_POS",
                   integerDiv(INV_START + INV_END, 2));

    // Genome
    initializeGenomicElementType("g1", c(m1, m2), c(0.26, 0.74));
    initializeGenomicElement(g1, 0, L - 1);

    initializeMutationRate(MU_TOTAL);
    initializeRecombinationRate(REC_RATE);
}

1 early() {
    sim.addSubpop("p1", K_ANCESTRAL);
    defineConstant("simID", getSeed());
}

1:BURN_IN_GENS late() {
    if (sim.generation % 1000 == 0) {
        meanFit = mean(p1.cachedFitness(NULL));
        catn("Gen: " + sim.generation +
             " | Mean Fitness: " + meanFit);
    }
}

BURN_IN_GENS late() {
    catn("Burn-in complete");
    sim.outputFull("burnin_state_K=" + K_ANCESTRAL +
                   "_simID=" + simID + ".txt");
    sim.simulationFinished();
}
